/**
 * Implements the Snake game.
 * In this game you need to avoid of the screen borders by moving
 * the snake with the keyboard arrows. Press 'q' to exit the game.
 */
class SnakeGame {
	
	/** The snake */
	field Snake snake;

	field int gamesNum;

	/** Constructs a new Snake Game. */
	constructor SnakeGame new(int snakeLength, int snakeWidth) {
		let snake = Snake.new(snakeLength, snakeWidth);
		let gamesNum = 0;
		do printTitle();

		return this;
	}

	/** Print the game title. */
	method void printTitle() {
		// S
		do Screen.drawRectangle(20, 20, 80, 30);
		do Screen.drawRectangle(20, 20, 30, 55);
		do Screen.drawRectangle(20, 45, 80, 55);
		do Screen.drawRectangle(70, 45, 80, 80);
		do Screen.drawRectangle(20, 70, 80, 80);
		do Sys.wait(300);

		// N
		do Screen.drawRectangle(90, 20, 100, 80);
		do Screen.drawRectangle(90, 20, 100, 30);
		do Screen.drawRectangle(100, 30, 110, 40);
		do Screen.drawRectangle(110, 40, 120, 50);
		do Screen.drawRectangle(120, 50, 130, 60);
		do Screen.drawRectangle(130, 60, 140, 70);
		do Screen.drawRectangle(140, 70, 150, 80);
		do Screen.drawRectangle(140, 20, 150, 80);
		do Sys.wait(300);

		// A
		do Screen.drawRectangle(160, 70, 170, 80);
		do Screen.drawRectangle(165, 60, 175, 70);
		do Screen.drawRectangle(170, 50, 180, 60);
		do Screen.drawRectangle(175, 40, 185, 50);
		do Screen.drawRectangle(180, 30, 190, 40);
		do Screen.drawRectangle(185, 20, 195, 30);
		do Screen.drawRectangle(170, 50, 210, 60);
		do Screen.drawRectangle(210, 70, 220, 80);
		do Screen.drawRectangle(205, 60, 215, 70);
		do Screen.drawRectangle(200, 50, 210, 60);
		do Screen.drawRectangle(195, 40, 205, 50);
		do Screen.drawRectangle(190, 30, 200, 40);
		do Sys.wait(300);

		// K
		do Screen.drawRectangle(230, 20, 240, 80);
		do Screen.drawRectangle(235, 45, 245, 55);
		do Screen.drawRectangle(245, 35, 255, 45);
		do Screen.drawRectangle(245, 55, 255, 65);
		do Screen.drawRectangle(255, 25, 265, 35);
		do Screen.drawRectangle(255, 65, 265, 75);
		do Screen.drawRectangle(265, 20, 275, 30);
		do Screen.drawRectangle(265, 70, 275, 80);
		do Sys.wait(300);

		// E
		do Screen.drawRectangle(290, 20, 300, 80);
		do Screen.drawRectangle(290, 20, 350, 30);
		do Screen.drawRectangle(290, 45, 350, 55);
		do Screen.drawRectangle(290, 70, 350, 80);
		do Sys.wait(300);

		// ! ! !
		do Screen.drawRectangle(370, 15, 380, 70);
		do Screen.drawRectangle(370, 75, 380, 85);
		do Sys.wait(150);

		do Screen.drawRectangle(390, 15, 400, 70);
		do Screen.drawRectangle(390, 75, 400, 85);
		do Sys.wait(150);

		do Screen.drawRectangle(410, 15, 420, 70);
		do Screen.drawRectangle(410, 75, 420, 85);
		do Sys.wait(750);

		return;
	}

	/** Print the enter screen masssge. */
	method void enterScreen() {

		do Screen.clearScreen();
		do Output.moveCursor(11,27);
		do Output.printString("Get Ready ...");

		do Sys.wait(750);

		do Screen.clearScreen();
		do Output.moveCursor(11,29);
		do Output.printString("Set ...");

		do Sys.wait(750);

		do Screen.clearScreen();
		do Output.moveCursor(11,29);
		do Output.printString("GO !!!");

		do Sys.wait(750);
		do Screen.clearScreen();

		return;
	}

	/** 
	 * Starts the game. Handles inputs from the user that control
     * the snake's movement. 
     */
	method void run() {
		var char key;
		var boolean exit;
		var boolean continue;
		var int direction;

		let continue = true;
		let exit = false;
		let direction = 0;

		do enterScreen();

		while(~exit) {

			// waits for a key to be pressed.
			while(key = 0) {
                let key = Keyboard.keyPressed();
                let continue = snake.updateSnake(direction);
                if(~continue) {
                	do gameOver();
                	return;
                }
            }

            if(key = 81) {
            	let gamesNum = gamesNum + 1;
            	let exit = true;
            }

			// move right
			if(key = 132) {
				if(~(direction = 2)) {
					let direction = 0;
				}
			}

			// move down
			if(key = 133) {
				if(~(direction = 3)) {
					let direction = 1;
				}
			}

			// move left
			if(key = 130) {
				if(~(direction = 0)) {
					let direction = 2;
				}
			}

			// move up
			if(key = 131) {
				if(~(direction = 1)) {
					let direction = 3;
				}
			}

			// waits for the key to be released.
			while (~(key = 0)) {
                let key = Keyboard.keyPressed();
                let continue = snake.updateSnake(direction);
                if(~continue) {
                	do gameOver();
                	return;
                }
            }

		}

		do printEndGame();

		return;
	}

	/** Print a massege of ending the game. */
	method void printEndGame() {

		do Screen.clearScreen();
		do Output.moveCursor(11,24);
		do Output.printString("You played ");
		do Output.printInt(gamesNum);
		do Output.printString(" Games.");
		do Sys.wait(500);
		do Output.moveCursor(13,23);
		do Output.printString("Thank You! Bye Bye ...");

		return;
	}

	/** Print game over massege. */
	method void gameOver() {
		var char key;

		let gamesNum = gamesNum + 1;

		do Screen.clearScreen();
		do Output.moveCursor(10,27);
		do Output.printString("Game Over !!!");
		do Sys.wait(500);
		do Output.moveCursor(12,27);
		do Output.printString("Another Game?");
		do Output.moveCursor(13,25);
		do Output.printString("(y - yes, n - no)");

		while(true) {
			let key = Keyboard.keyPressed();
			if(key = 89) { // yes
				do snake.initSnake();
				do run();
				return;
			}
			if(key = 78) { // no
				do printEndGame();
				return;
			}
		}

		return;
	}

	/** Deallocates the object's memory. */
	method void dispose() {

		do snake.disposeSnake();
		do Memory.deAlloc(this);

		return;
	}

}